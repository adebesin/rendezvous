import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

buildscript {
    ext {
        springBootVersion = '2.1.4.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.bmuschko:gradle-docker-plugin:4.6.2'
        classpath "gradle.plugin.com.avast.gradle:gradle-docker-compose-plugin:0.9.4"
    }
}

apply plugin: "com.avast.gradle.docker-compose"

allprojects {
    repositories {
        mavenCentral()
        maven {
            url 'http://repository.mapr.com/maven'
        }
    }

    apply plugin: 'com.bmuschko.docker-remote-api'
}

wrapper {
    gradleVersion = '4.10.3'
}

task buildImage(type: DockerBuildImage) {
    inputDir = project.file('docker')
    tags = ["mapr/core:latest"]
    Map args = new HashMap()

    if (project.hasProperty('MAPR_CONTAINER_USER'))
        args.put('MAPR_CONTAINER_USER', project.MAPR_CONTAINER_USER)

    if (project.hasProperty('MAPR_CONTAINER_UID'))
        args.put('MAPR_CONTAINER_UID', project.MAPR_CONTAINER_UID)

    if (project.hasProperty('MAPR_CONTAINER_GROUP'))
        args.put('MAPR_CONTAINER_GROUP', project.MAPR_CONTAINER_GROUP)

    if (project.hasProperty('MAPR_CONTAINER_GID'))
        args.put('MAPR_CONTAINER_GID', project.MAPR_CONTAINER_GID)
    
    buildArgs = args
}

dockerCompose {
    useComposeFiles = ['docker/docker-compose.yml']
    projectName = 'rendezvous'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    buildscript {
        ext {
            springBootVersion = '2.1.4.RELEASE'
        }
        repositories {
            mavenCentral()
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
        dependencies {
            classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
            classpath "gradle.plugin.com.google.protobuf:protobuf-gradle-plugin:0.8.8"
            classpath 'com.bmuschko:gradle-docker-plugin:4.6.2'
        }
    }

    group = 'com.mapr.rendezvous'
    sourceCompatibility = '1.8'

    dependencies {
        implementation "org.springframework.boot:spring-boot-starter"
        implementation "org.projectlombok:lombok:1.18.4"
        implementation("org.apache.kafka:kafka-clients:1.1.1-mapr") {
            exclude group: 'org.slf4j', module: 'slf4j-log4j12'
            exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
        }
        implementation("com.mapr.streams:mapr-streams:6.1.0-mapr") {
            exclude group: 'org.slf4j', module: 'slf4j-log4j12'
            exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
        }
        implementation("io.projectreactor.kafka:reactor-kafka:1.1.0.RELEASE") {
            exclude group: 'org.slf4j', module: 'slf4j-log4j12'
            exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
            exclude group: 'org.apache.kafka', module: 'kafka-clients'
        }
        implementation "xerces:xercesImpl:2.11.0"
        testImplementation "org.springframework.boot:spring-boot-starter-test"
    }
}