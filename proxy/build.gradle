import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

version '1.0-SNAPSHOT'

apply plugin: "com.google.protobuf"

sourceSets {
    main {
        java {
            srcDir 'src/main/protoGen'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:2.5.0'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.2.0"
        }
    }

    generateProtoTasks {
        ofSourceSet('main').each { task ->
            task.builtins {
                java {
                    outputSubDir = 'protoGen'
                }
            }
            task.plugins {
                grpc {
                    outputSubDir = 'protoGen'
                }
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/"
}

task cleanProtoGen {
    doFirst {
        delete("$projectDir/src/main/protoGen")
    }
}

clean.dependsOn cleanProtoGen

configurations.all {
    resolutionStrategy {
        force 'com.google.protobuf:protobuf-java:2.5.0'
        force 'com.google.protobuf:protobuf-java-util:3.0.0'
    }
}

task createDockerfile(type: Dockerfile) {
    dependsOn bootJar
    destFile = project.file('build/libs/Dockerfile')
    from 'mapr/core:latest'
    copyFile jar.archiveName, '/app.jar'
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = project.file('build/libs')
    tags = ["mapr/proxy:latest"]
}

dependencies {
    implementation project(":commons")
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "io.grpc:grpc-stub:1.20.0"
    implementation "io.grpc:grpc-protobuf:1.20.0"
    implementation "io.grpc:grpc-netty-shaded:1.20.0"
    implementation "org.eclipse.jetty:jetty-webapp:9.4.16.v20190411"
}
